fmod CNF-SYNTAX is
  pr QID .

  sorts Literal Clause CNF .
  subsort Qid < Literal < Clause < CNF .

  var p : Literal .

  ops ⊤ ⊥ : -> Clause [ctor] .
  op ¬ _ : Literal -> Literal [ctor prec 25] .
  op _ ∨ _ : Clause Clause -> Clause [ctor assoc comm id: ⊥ prec 30] .
  op _ , _ : CNF CNF -> CNF [ctor assoc comm id: ⊤ prec 35] .

  eq p ∨ p = p .
  eq p ∨ ¬ p = ⊤ .
endfm

mod RESOLUTION is
  pr CNF-SYNTAX .

  sort State .
  subsort CNF < State .

  var p : Literal .
  vars C C' D0 D1 D2 : Clause .
  var ϕ : CNF .

  op resolved _ and _ on _ : Clause Clause Clause -> State [ctor prec 30] .
  op done : -> State [ctor] .

  rl [start] : ϕ => ϕ, resolved ⊥ and ⊥ on ⊥ .

  rl [resolve] :
    C ∨ p, C' ∨ ¬ p, resolved D0 and D1 on D2
  =>
    C ∨ C', resolved C ∨ p and C' ∨ ¬ p on p .

  rl [done] : ⊥, resolved D0 and D1 on D2 => done .
endm

smod PROOF-SEARCH-STRAT is
  pr RESOLUTION .

  strat proofSearch @ CNF .
  sd proofSearch := start ; resolve * ; done .
endsm

eof

set trace rl on .

srewrite
  'p ∨ 'q ∨ 'r, ¬ 'p ∨ 's, ¬ 'q ∨ 's, ¬ 'r ∨ 's, ¬ 's
  using proofSearch . 